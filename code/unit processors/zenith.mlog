read unit_on cell1 3
jump unbind_unit equal unit_on false
ubind @zenith
sensor cont @unit @controller
jump normal equal cont @unit
jump other_proc notEqual cont @this
normal:
write 0 cell1 22
jump start always 0 0
other_proc:
write 1 cell1 22
unbind_unit:	
ucontrol unbind 0 0 0 0 0
end

start:
sensor item_cap @unit @itemCapacity
sensor blast @unit @blast-compound
sensor items @unit @totalItems
jump unit_controls equal item_cap blast	
jump b_recup equal items blast
ucontrol itemDrop @air 999 0 0 0

b_recup:
ulocate building storage false @copper x y found vault
ucontrol itemTake vault @blast-compound 999 0 0
set radius 16
jump idle_pickup always 0 0

unit_controls:
sensor player_ctrl wave1 @controlled
jump player_controled equal player_ctrl @ctrlPlayer
read attack cell1 10
jump attacking equal attack 1

#idle in the center
set radius 40	#idle around middle / waiting
idle_pickup:	#idle but picking up blast
sensor center_idle_x cell1 @x
sensor center_idle_y cell1 @y
idle_around_cursor:
sensor unit_x @unit @x
sensor unit_y @unit @y
op sub dx unit_x center_idle_x
op sub dy unit_y center_idle_y
op sub px1 dx dy
op add py1 dx dy
op len p1_len px1 py1
op div p_len radius p1_len
op mul px p_len px1
op mul py p_len py1
op add next_pos_x center_idle_x px
op add next_pos_y center_idle_y py
ucontrol move next_pos_x next_pos_y
jump target_enemy_unit always 0 0

player_controled:
sensor target_x wave1 @shootX
sensor target_y wave1 @shootY
sensor always_dive switch1 @enabled
jump dive equal always_dive 1
sensor dive_on_target wave1 @shooting
jump dive equal dive_on_target true
set radius 20
set center_idle_x target_x
set center_idle_y target_y
jump idle_around_cursor always 0 0
dive:
ucontrol move target_x target_y
ucontrol target target_x target_y 1 0 0
end

attacking:
ulocate building turret true @copper outx outy found building
sensor attack_defences_only switch2 @enabled
jump attack_building equal attack_defences_only true
ucontrol within outx outy 30 turret_in_range 0		#30 is the range to clear around a core before attacking it
jump attack_building equal turret_in_range true
ulocate building core true @copper outx outy found building		#override outx & outy with core pos
attack_building:
ucontrol move outx outy 0 0 0

sensor range @unit @range		#target an enemy if the block target is not in range
ucontrol within outx outy range target_in_range 0 
jump target_enemy_unit equal target_in_range false

ucontrol target outx outy 1 0		#target the turret
end

target_enemy_unit:
uradar enemy any any health 0 1 enemy
ucontrol targetp enemy 1 1 0 0